INCLUDE_DIRECTORIES(${CMAKE_CURRENT_SOURCE_DIR})

SET(Intrepid2_TEST_ETI_FILE "test_01;test_02;test_03;test_04;test_05")

SET(Intrepid2_TEST_ETI_DEVICE_NAME "")
IF(Kokkos_ENABLE_SERIAL)
  LIST(APPEND Intrepid2_TEST_ETI_DEVICE_NAME "Serial")
ENDIF()
IF(Kokkos_ENABLE_OPENMP)
  LIST(APPEND Intrepid2_TEST_ETI_DEVICE_NAME "OpenMP")
ENDIF()
IF(Kokkos_ENABLE_CUDA)
  LIST(APPEND Intrepid2_TEST_ETI_DEVICE_NAME "CUDA")
ENDIF()
IF(Kokkos_ENABLE_HIP)
  LIST(APPEND Intrepid2_TEST_ETI_DEVICE_NAME "HIP")
ENDIF()

FOREACH(ETI_DEVICE_NAME IN LISTS Intrepid2_TEST_ETI_DEVICE_NAME)
  FOREACH(ETI_FILE IN LISTS Intrepid2_TEST_ETI_FILE)
    IF (${ETI_DEVICE_NAME} STREQUAL "Serial")
      SET(ETI_DEVICE "Kokkos::Device<Kokkos::Serial,Kokkos::HostSpace>")
    ELSEIF (${ETI_DEVICE_NAME} STREQUAL "OpenMP")
      SET(ETI_DEVICE "Kokkos::Device<Kokkos::OpenMP,Kokkos::HostSpace>")
    ELSEIF (${ETI_DEVICE_NAME} STREQUAL "CUDA")
      SET(ETI_DEVICE "Kokkos::Device<Kokkos::Cuda,Kokkos::CudaSpace>")
    ELSEIF (${ETI_DEVICE_NAME} STREQUAL "HIP")
      SET(ETI_DEVICE "Kokkos::Device<Kokkos::Experimental::HIP,Kokkos::Experimental::HIPSpace>")
    ENDIF()
    SET(ETI_NAME "${ETI_FILE}_${ETI_DEVICE_NAME}")
    MESSAGE(STATUS "Generating TEST: ArrayTools ${ETI_NAME}.cpp")
    CONFIGURE_FILE(eti/${ETI_FILE}_ETI.in ${ETI_NAME}.cpp)

    TRIBITS_ADD_EXECUTABLE_AND_TEST(
      ${ETI_NAME}
      SOURCES ${ETI_NAME}.cpp 
      ARGS PrintItAll
      NUM_MPI_PROCS 1
      PASS_REGULAR_EXPRESSION "TEST PASSED"
      ADD_DIR_TO_NAME
    )

  ENDFOREACH()
ENDFOREACH()
